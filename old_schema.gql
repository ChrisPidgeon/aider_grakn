define 

person sub entity, 
	owns email, 
	owns name,
	owns phone,
	owns age,
	owns gender,
	owns title,
	plays support-ticket-submission:submitted,
	plays device-session:active-person,
	plays debit-transaction:transacting,
	plays application:applying, 
	plays marriage:husband, 
	plays marriage:wife,
	plays employment:employed, 
	plays product-ownership:product-holder, 
	plays visit:visiting,
	plays promotion:receiving, 
	plays liking:reacted-by, 
	plays reply:replied-by,
	plays recommendation:recommended-to,
	plays negative-recommendation:negative-recommended-to,
	plays withdraw:withdrawing; 
company sub entity,
	owns name,
	plays employment:employer;
bank-branch sub entity,
	plays branch-visit:visited-branch,
	plays location:located;
atm sub entity, 
	plays withdraw:withdrawing-atm;
product sub entity, 
	owns product-name, 
	plays recommendation:recommended-product,
	plays negative-recommendation:negative-recommended-product,
	plays support-ticket-submission:ticketed-product,
	plays product-ownership:owned-product,
	plays debit-transaction:account-transaction,
	plays search:searched-product,
	plays promotion:promoted;
current-account sub product,
	owns account-balance; 
mortgage sub product; 
credit-card sub product;
web-page sub entity,
	owns website-title, 
	plays promotion:promoting-page,
	plays website-visit:visited-website; 
post sub entity, 
	owns post-title, 
	owns text, 
	plays reply:replied-to, 
	plays liking:reacted-to,
	plays promotion:promoting-post; 
comment sub post, 
	plays reply:reply-content;
mortgage-application sub entity, 
	owns application-status, 
	owns application-filed, 
	plays application:applied;
data-source sub entity, 
	owns name,
	plays ownership:owned,
	plays source:sourcing; 
bank sub company;
subsidiary sub bank,
	plays ownership:owner;
city sub entity, 
	owns name,
	plays location:locating; 
transaction sub entity, 
	owns amount,
	plays debit-transaction:transacted, 
	plays location:located; 
support-ticket sub entity, 
	owns ticket-title,
	plays support-ticket-submission:ticketing; 
device sub entity,
	owns device-type, 
	plays device-session:active-device;
session sub entity,
	owns session-duration, 
	owns timestamp,
	owns session-id,
	plays website-visit:visiting,
	plays device-session:active-session,
	plays search:active-search-session;
employment sub relation, 
	relates employed,
	relates employer;
visit sub relation, 
	relates visiting;
branch-visit sub relation,
	relates visited-branch,
	relates visiting,
	owns visit-date;
website-visit sub relation,
	relates visited-website,
	relates visiting,
	owns visit-date;
source sub relation, 
	relates sourced, 
	relates sourcing; 
ownership sub relation, 
	relates owned, 
	relates owner; 
application sub relation, 
	relates applied, 
	relates applying; 
product-ownership sub relation, 
	relates product-holder,
	relates owned-product;
promotion sub relation,
	relates receiving,
	relates promoted,
	relates promoting-post,
	relates promoting-page, 
	owns link-clicks,
	owns promotion-name,
	owns opens;
website-promotion sub promotion;
email-promotion sub promotion;
social-promotion sub promotion;
reply sub relation,
	relates replied-to,
	relates reply-content,
	relates replied-by;
withdraw sub relation,
	relates withdrawing-atm,
	relates withdrawing,
	owns withdraw-amount;
liking sub relation, 
	relates reacted-to,
	relates reacted-by;
marriage sub relation, 
	plays source:sourced,
	relates husband,
	relates wife;
recommendation sub relation, 
	relates recommended-product,
	relates recommended-to;
negative-recommendation sub relation, 
	relates negative-recommended-product,
	relates negative-recommended-to;
device-session sub relation, 
	relates active-session,
	relates active-device,
	relates active-person;
search sub relation, 
	relates active-search-session, 
	relates searched-product;
support-ticket-submission sub relation, 
	relates ticketing, 
	relates ticketed-product,
	relates submitted;
debit-transaction sub relation, 
	relates account-transaction,
	relates transacting, 
	relates transacted;
location sub relation,
	relates located,
	relates locating; 

email sub attribute, value string;
name sub attribute, value string;
product-name sub attribute, value string;
account-balance sub attribute, value double;
website-title sub attribute, value string;
post-title sub attribute, value string;
text sub attribute, value string;
visit-date sub attribute, value string;
opens sub attribute, value double;
link-clicks sub attribute, value double;
withdraw-amount sub attribute, value double;
promotion-name sub attribute, value string;
gender sub attribute, value string;
phone sub attribute, value string;
age sub attribute, value string;
title sub attribute, value string;
application-status sub attribute, value string;
application-filed sub attribute, value string;
amount sub attribute, value string;
ticket-title sub attribute, value string;
device-type sub attribute, value string;
session-duration sub attribute, value string;
timestamp sub attribute, value string;
session-id sub attribute, value string;

negative-product-recommendation sub rule, 
when {
	$p isa person;
	$pr isa product;
	$1 (receiving: $p, promoted: $pr) isa promotion, has opens $o;
	$2 (receiving: $p, promoted: $pr) isa promotion, has opens $o2;
	$1 != $2; 
	$o == 0; 
	$o2 == 0;
}, then {
	(negative-recommended-product: $pr, negative-recommended-to: $p) isa negative-recommendation;
};

product-recommendation sub rule,
when {
	$p isa person;
	$pr isa product;
	$w isa web-page;
	$sess isa session; 
	$0 (active-session: $sess, active-device: $dev, active-person: $p) isa device-session; 
	$1 (visiting: $sess, visited-website: $w) isa website-visit;
	$2 (promoting-page: $w, promoted: $pr) isa website-promotion;
	$post isa post;
	$sp (promoting-post: $post, promoted: $pr) isa social-promotion;
	$com isa comment;
	$reply (replied-to: $post, replied-by: $p, reply-content: $com) isa reply;
	$liking (reacted-to: $post, reacted-by: $p) isa liking; 
}, then {
	(recommended-product: $pr, recommended-to: $p) isa recommendation;
};

mortgage-marriage-recommendation sub rule,
when {
	$p isa person;
	$p2 isa person;
	$2 ($p2, $p) isa marriage; 
	$pr isa mortgage;
	$1 (recommended-product: $pr, recommended-to: $p2) isa recommendation;
}, then {
	(recommended-product: $pr, recommended-to: $p) isa recommendation;
};

insert

$p2 isa person, has name "Bob", has email "bob@gmail.com", has gender "male", has age "31", has phone "024929404", has title "Mr"; 
$c2 isa current-account, has account-balance 2039, has product-name "Current Account Advantage"; 
(product-holder: $p2, owned-product: $c2) isa product-ownership;

$p isa person, has name "Susan", has email "susan@gmail.com", has gender "female", has age "29", has phone "023224593", has title "Mrs"; 
$marr (husband: $p2, wife: $p) isa marriage; 

(sourced: $marr, sourcing: $data) isa source; 
$data isa data-source, has name "Marriages-DB 2012";
(owned: $data, owner: $subsidiary) isa ownership; 
$subsidiary isa subsidiary, has name "NextGen Subsidiary"; 

$c isa current-account, has account-balance 9293, has product-name "Current Account Plus"; 
(product-holder: $p, owned-product: $c) isa product-ownership;

$atm isa atm;
(withdrawing-atm: $atm, withdrawing: $p) isa withdraw, has withdraw-amount 100;

$cc isa credit-card, has product-name "10k Credit Cards"; 
(promoted: $cc, receiving: $p) isa email-promotion, has opens 0, has link-clicks 0, has promotion-name "Join our best credit cards!";
(promoted: $cc, receiving: $p) isa email-promotion, has opens 0, has link-clicks 0, has promotion-name "Promotional offer on credit cards available today!";

$w isa web-page, has website-title "Mortgage Product Overview"; 
(visiting: $sess, visited-website: $w) isa website-visit;
(promoting-page: $w, promoted: $m) isa website-promotion;

$m isa mortgage, has product-name "Affordable Housing Mortgage"; 
(promoted: $m, receiving: $p) isa email-promotion, has opens 3, has link-clicks 6, has promotion-name "Amazing Mortgages!";

$post isa post, has post-title "Is this your dream home?";
(promoting-post: $post, promoted: $m) isa social-promotion;
$com isa comment, has text "I love this house!!! I want to buy a house soon!";
(replied-to: $post, replied-by: $p, reply-content: $com) isa reply;
(reacted-to: $post, reacted-by: $p) isa liking; 

$city isa city, has name "Liverpool"; 
$city2 isa city, has name "Manchester"; 

$br isa bank-branch; 
(located: $br, locating: $city) isa location; 
$br2 isa bank-branch;
(located: $br2, locating: $city2) isa location; 
(visited-branch: $br, visiting: $p2) isa branch-visit, has visit-date "7 Oct 2018"; 
(visited-branch: $br2, visiting: $p2) isa branch-visit, has visit-date "19 Sept 2019"; 
(visited-branch: $br, visiting: $p) isa branch-visit, has visit-date "7 Oct 2018"; 
(visited-branch: $br2, visiting: $p) isa branch-visit, has visit-date "19 Sept 2019"; 

$appli isa mortgage-application, has application-status "Refused", has application-filed "1 Nov 2014"; 
(applying: $p2, applied: $appli) isa application; 

$sess isa session, has session-duration "2401", has timestamp "1389202121", has session-id "340BF0ED-77B0-45D0-A81EBE2F69DB51C1"; 
$dev isa device, has device-type "Samsung S10"; 
(active-session: $sess, active-device: $dev, active-person: $p) isa device-session; 
(active-search-session: $sess, searched-product: $m) isa search; 

$spt isa support-ticket, has ticket-title "My transaction got declined"; 
(ticketing: $spt, ticketed-product: $c, submitted: $p) isa support-ticket-submission; 

$tran isa transaction, has amount "94"; 
$tran1 isa transaction, has amount "13"; 
$tran2 isa transaction, has amount "932"; 
(account-transaction: $c, transacting: $p, transacted: $tran) isa debit-transaction; 
(account-transaction: $c, transacting: $p, transacted: $tran1) isa debit-transaction; 
(account-transaction: $c, transacting: $p, transacted: $tran2) isa debit-transaction; 
(located: $tran, locating: $city) isa location; 
(located: $tran1, locating: $city) isa location; 
(located: $tran2, locating: $city) isa location; 